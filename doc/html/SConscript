import os.path

Import('env', 'bin', 'pyext')

# Build DocBook documentation:
def action_docbook(target, source, env):
    outdir = os.path.dirname(target[0].path)
    app = "xmlto -o %s -m doc/html-style.xml xhtml %s" % (outdir,
                                                          source[0].path)
    return env.Execute(app)

def emit_docbook(target, source, env):
    for xml in ('acknowl.xml', 'html-style.xml', 'study.xml', 'usage.xml'):
        source.append(os.path.join('..', xml))
    return target, source

builder_docbook = Builder(action=action_docbook, emitter=emit_docbook)

env.Append(BUILDERS = {'Docbook': builder_docbook})

docbook = env.Docbook("index.html", "../manual.xml")
epydoc = env.Epydoc('epydoc/index.html', (bin, pyext))

docdir = env['docdir']
env.Command(os.path.join(docdir, 'index.html'), docbook,
            "install -d ${TARGET.dir} && " + \
            "install ${SOURCE.dir}/*.html ${SOURCE.dir}/*.css ${TARGET.dir}")
env.Alias('docinstall', docdir)

docdir = os.path.join(docdir, 'epydoc')
# Install all files from 'epydoc' directory into the 'docdir' directory (cannot
# use env.InstallAs() right now, due to scons bug #1751)
env.Command(os.path.join(docdir, 'index.html'), epydoc,
            "install -d ${TARGET.dir} && install ${SOURCE.dir}/* ${TARGET.dir}")

# Subdirectories:
SConscript('pdf/SConscript')
