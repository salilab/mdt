Import('env', 'get_pyext_environment')

# Get a modified build environment suitable for building Python extensions:
e = get_pyext_environment(env, 'mdt')
e.MergeFlags(e['MODELLER'])

e.Append(LIBPATH=['../src'])
e.Append(LIBS=['mdt'])
if e['PLATFORM'] == 'win32':
    e.Append(SHLINKFLAGS=' glib-2.0.lib')
else:
    e.ParseConfig('pkg-config --cflags --libs glib-2.0')

# Build the Python extension from SWIG interface file:
pyso = e.LoadableModule('_mdt', 'mdt.i',
                        SWIGFLAGS='-python -noproxy ' + \
                                  '-nodefaultctor -nodefaultdtor')

# Subdirectories containing pure Python files:
Export('e')
pyext = SConscript('mdt/SConscript')

# Install the Python extension:
libinst = e.Install(e['destdir'] + e['pyextdir'], pyso)
e.Alias('install', libinst)

Return('pyso', 'pyext')
