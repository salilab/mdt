Import('env', 'modconfig', 'pythoninc', 'configure_for_pyext',
       'is_wine_platform')

# Get a modified build environment suitable for building Python extensions:
e = env.Copy()
if not is_wine_platform(modconfig):
    e.ParseConfig('pkg-config --cflags glib-2.0')
e.Append(CPPPATH=modconfig['include'] + [pythoninc])
e.Append(LIBPATH=['../src'] + modconfig['libpath'])
e.Append(LIBS=modconfig['libs'] + ['mdt'])
e = configure_for_pyext(e, modconfig)

# On Mac, add an explicit dependency on the C shared library, since this
# isn't done by default:
if modconfig['exetype'].startswith('mac10'):
    e.Depends('_mdt.so', '#src/libmdt.dylib')

# Build the Python extension from SWIG interface file:
lib = e.SharedLibrary('_mdt', 'mdt.i',
                      SWIGFLAGS='-python -noproxy ' + \
                                '-nodefaultctor -nodefaultdtor')

# Install the Python extension and any .py files:
libinst = e.Install(modconfig['libinstall'], lib)
pyinst = e.Install(modconfig['pyinstall'], 'mdt.py')
e.Alias('install', [libinst, pyinst])
Return('lib')
