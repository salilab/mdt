Import('env', 'modconfig', 'get_pyext_environment', 'is_wine_platform')

# Get a modified build environment suitable for building Python extensions:
e = get_pyext_environment(env)
if is_wine_platform(modconfig):
    e.Append(SHLINKFLAGS=' glib-2.0.lib')
else:
    e.ParseConfig('pkg-config --cflags --libs glib-2.0')
e.Append(CPPPATH=modconfig['include'])
e.Append(LIBPATH=['../src'] + modconfig['libpath'])
e.Append(LIBS=modconfig['libs'] + ['mdt'])

# On Mac, add an explicit dependency on the C shared library, since this
# isn't done by default:
if modconfig['exetype'].startswith('mac10'):
    e.Depends('_mdt.so', '#src/libmdt.dylib')

# Build the Python extension from SWIG interface file:
pyso = e.SharedLibrary('_mdt', 'mdt.i',
                       SWIGFLAGS='-python -noproxy ' + \
                                 '-nodefaultctor -nodefaultdtor')

pyext = File('mdt.py')
# Install the Python extension and any .py files:
libinst = e.Install(modconfig['libinstall'], pyso)
pyinst = e.Install(modconfig['pyinstall'], pyext)
e.Alias('install', [libinst, pyinst])
Return('pyso', 'pyext')
