Import('env', 'modconfig', 'bin')

e = env.Copy()

if modconfig['exetype'] != 'i386-w32':
    python = 'python'
else:
    # Link the shared library into the current directory so that Windows can
    # find it:
    import shutil
    e.Command('#libmdt.dll', '#src/libmdt.dll', 'ln -sf $SOURCE $TARGET')
    e.Depends('test.passed', '#libmdt.dll')
    python = 'w32python'

# Custom builder to run unit tests:
def builder_unit_test(target, source, env):
    app = "%s %s %s -v > /dev/null" % (bin[0], python, source[0])
    if env.Execute(app) == 0:
        file(str(target[0]), 'w').write('PASSED\n')
    else:
        print "unit tests FAILED"
        return 1
e.Append(BUILDERS = {'Test': Builder(action=builder_unit_test)})

# Always run our unit test:
test = e.Test("test.passed", "run-all-tests.py")
e.Depends(test, bin)
e.Alias("test", test)
e.AlwaysBuild("test.passed")
Return('test')
